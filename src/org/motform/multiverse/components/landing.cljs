(ns org.motform.multiverse.components.landing
  (:require [clojure.string :as str]
            [re-frame.core :as rf]
            [reagent.core :as r]))

(defn circle [r cx cy]
  (let [filled? (when (< 2 (rand-int 5)) {:fill "var(--bg2)" :class "filled"})]
    (when (< 1 (rand-int 5))
      [:circle.circle
       (merge {:r r :cx cx :cy cy :stroke "var(--bg2)" :stroke-width "1" :fill "none"}
              filled?)])))

(defn circles []
  (let [wh (quot (. js/window -innerHeight) 2.7) ; arbitrary
        ww (. js/window -innerWidth)
        r  (dec (quot wh 6))] ; dec to properly fit three circles with gaps
    [:div.circles-container>svg.circles
     (for [cy (range (inc r) (+ 200 wh) (+ 2 (* r 2)))  ; three rows
           cx (range 0       (inc ww) (+ 2 (* r 2)))] ; fills the rest
       ^{:key (str cx cy)} [circle r cx cy])]))

(defn elide [s n dots]
  (apply str (concat (take n s) (repeat dots ".") (drop (- (count s) n) s))))

(defn key-input-textarea [api-key validated?]
  [:textarea.key-input.shadow-medium.textarea-small.rounded.mono
   {:value        (if (< 10 (count api-key)) (elide api-key 3 20) api-key)
    :spell-check  false
    :class        (when validated? "key-valid")
    #_(when (not (str/blank? api-key))
        (str "key-" (when-not validated? "in") "valid"))
    :on-change    #(rf/dispatch [:open-ai/update-api-key (.. % -target -value)])
    :on-key-down  #(when (= (.-key %) "Enter")
                     (.preventDefault %)
                     (rf/dispatch [:open-ai/validate-api-key]))}])

(defn name-input-textarea [name]
  [:textarea.author.shadow-medium.textarea-small.rounded
   {:value name
    :on-change #(rf/dispatch [:name (-> % .-target .-value)])}])

(defn key-input []
  (let [{:keys [api-key validated?]} @(rf/subscribe [:open-ai])
        name @(rf/subscribe [:name])
        disabled? (some str/blank? [name api-key])]
    [:article.key-input-container.v-stack.gap-full.rounded-large.shadow-large
     [:section.v-stack.gap-half
      [:div.v-stack.gap-quarter 
       [:label.offset-label "Name"]
       [name-input-textarea name]]
      [:div.v-stack.gap-quarter 
       [:label.offset-label "OpenAI API key"]
       [key-input-textarea api-key validated?]]] 
     [:button.open-ai-key-dispatch.rounded
      {:disabled disabled?
       :on-pointer-down #(if (not validated?)
                           (rf/dispatch [:open-ai/validate-api-key])
                           (rf/dispatch [:active-page :new-story]))}
      (if validated?
        "start"
        "validate key")]]))

(defn landing-blurb []
  [:section.landing-blurb.v-stack.gap-half
   [:p "Multiverse is a prototype system for interactive generative literature. It allows to user-reader to non-linearly explore a literary space generated by OpenAI's GPT family of machine learning langauge models. Read more "[:a {:href "https://motform.org/multiverse"} "here"] "."]
   [:p "The system is requires an " [:a {:href "https://openai.com/api/"} "OpenAI API key"] " to run. You will have to provide your own unless otherwise specified. Multiverse does not store your key in any way."]])

(defn landing []
  [:<>
   [circles]
   [:div.section.landing.v-stack.gap-double
    [:div.v-stack.gap-eight 
     [:h1.landing-nameplate "MULTIVERSE"]
     [:p.landing-subtitle "Cybertextual generative literature through machine learning"]]
    [:section.landing-container.h-stack.gap-double
     [landing-blurb]
     [key-input]]]])
